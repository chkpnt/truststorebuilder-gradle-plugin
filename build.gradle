apply plugin: 'groovy'
apply plugin: 'java'

group = 'de.chkpnt'
description = "Building Java TrustStores with Gradle."

def setToDefaultIfNotExists(property, defaultValue) {
	if (!project.hasProperty(property)) {
		project.ext.setProperty(property, defaultValue)
	}
}

repositories {
	jcenter()
}

compileGroovy {
	groovyOptions.encoding = 'UTF-8'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

// Write the plugin's classpath to a file to share with the tests
task createClasspathManifest {
	def outputDir = file("$buildDir/$name")

	inputs.files sourceSets.main.runtimeClasspath
	outputs.dir outputDir

	doLast {
		outputDir.mkdirs()
		file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
	}
}

dependencies {
	compile gradleApi()
	compile localGroovy()
	
	testCompile gradleTestKit()
	testRuntime files(createClasspathManifest)
	testCompile 'commons-io:commons-io:2.2'
	testCompile 'com.google.jimfs:jimfs:1.0'      // In-Memory Filesystem
	testCompile 'org.spockframework:spock-core:1.0-groovy-2.4', {
		exclude group: 'org.codehaus.groovy' }
	testCompile 'cglib:cglib-nodep:3.2.0'
	testCompile 'org.hamcrest:hamcrest-all:1.3'
}

task wrapper(type: Wrapper) {
	gradleVersion = '4.4.1'
	distributionUrl = 'https://services.gradle.org/distributions/gradle-4.4.1-all.zip'
}

apply from: 'gradle/versioning.gradle'
apply from: 'gradle/github.gradle'
apply from: 'gradle/docs.gradle'
apply from: 'gradle/publish.gradle'
apply from: 'gradle/spotless.gradle'
apply from: 'gradle/qa.gradle'
