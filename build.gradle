plugins {
	id "java-gradle-plugin"
	id "groovy"
	id "org.jetbrains.kotlin.jvm" version "1.5.31" // should match Kotlin version bundled with Gradle
	id "truststorebuilder-versioning"
	id "truststorebuilder-docs"
	id "truststorebuilder-publish"
	id "truststorebuilder-spotless"
	id "truststorebuilder-qa"
}

group = 'de.chkpnt'
description = "Building Java TrustStores with Gradle."

repositories {
	mavenCentral()
}

compileGroovy {
	groovyOptions.encoding = 'UTF-8'
}

java {
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8
}

compileKotlin.kotlinOptions.jvmTarget = "1.8"
compileTestKotlin.kotlinOptions.jvmTarget = "1.8"

gradlePlugin {
	plugins {
		truststorebuilder {
			id = 'de.chkpnt.truststorebuilder'
			implementationClass = 'de.chkpnt.gradle.plugin.truststorebuilder.TrustStoreBuilderPlugin'
		}
	}
}

dependencies {
	compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

	testImplementation localGroovy()
	testImplementation gradleTestKit()
	testImplementation 'commons-io:commons-io:2.9.0'
	testImplementation 'com.google.jimfs:jimfs:1.2'      // In-Memory Filesystem
	testImplementation 'org.spockframework:spock-core:2.0-groovy-3.0', {
		exclude group: 'org.codehaus.groovy'
	}
	testImplementation 'org.hamcrest:hamcrest-all:1.3'
	testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
}

test {
	useJUnitPlatform()
}

wrapper {
	gradleVersion = '7.3.3'
	distributionType = 'all'
}

