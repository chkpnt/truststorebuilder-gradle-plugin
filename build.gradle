apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'jacoco'

group = 'de.chkpnt'
version = '1.0.0'

repositories {
	jcenter()
}

compileGroovy {
	groovyOptions.encoding = 'UTF-8'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

jacocoTestReport {
	reports {
		xml.enabled = true
		html.enabled = true
	}
	
	// Gradle's Test-Kit doesn't support JaCoCo right now.
	// This feature is targeted in Milestone 3:
	// https://github.com/gradle/gradle/blob/master/design-docs/testing-toolkit.md
	// I'm excluding the corresponding test, until this feature is available.
	afterEvaluate {
		classDirectories = files(classDirectories.files.collect {
			fileTree(dir: it, exclude: '**/TrustStoreBuilderPlugin*.class')
        })
    }
}

check.dependsOn jacocoTestReport

// Write the plugin's classpath to a file to share with the tests
task createClasspathManifest {
	def outputDir = file("$buildDir/$name")

	inputs.files sourceSets.main.runtimeClasspath
	outputs.dir outputDir

	doLast {
		outputDir.mkdirs()
		file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
	}
}

dependencies {
	compile gradleApi()
	compile localGroovy()
	
	testCompile gradleTestKit()
	testRuntime files(createClasspathManifest)
	testCompile 'com.google.jimfs:jimfs:1.0'      // In-Memory Filesystem
	testCompile 'org.spockframework:spock-core:1.0-groovy-2.4', {
		exclude group: 'org.codehaus.groovy' }
//		exclude group: 'org.hamcrest' }
	testCompile 'org.hamcrest:hamcrest-all:1.3'
}



task wrapper(type: Wrapper) {
	gradleVersion = '2.9'
	distributionUrl = 'https://services.gradle.org/distributions/gradle-2.9-all.zip'
}

apply from: 'gradle/publish.gradle'
apply from: 'gradle/eclipse.gradle'
