buildscript {
	repositories.maven { url 'https://plugins.gradle.org/m2/' }
	//repositories { mavenCentral() }
	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.71"
	}
}

apply plugin: 'groovy'
apply plugin: 'kotlin'
apply plugin: 'java-gradle-plugin'

group = 'de.chkpnt'
description = "Building Java TrustStores with Gradle."

def setToDefaultIfNotExists(property, defaultValue) {
	if (!project.hasProperty(property)) {
		project.ext.setProperty(property, defaultValue)
	}
}

repositories {
	jcenter()
}

compileGroovy {
	groovyOptions.encoding = 'UTF-8'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
compileKotlin.kotlinOptions.jvmTarget = "1.8"
compileTestKotlin.kotlinOptions.jvmTarget = "1.8"

gradlePlugin {
	plugins {
		truststorebuilder {
			id = 'de.chkpnt.truststorebuilder'
			implementationClass = 'de.chkpnt.gradle.plugin.truststorebuilder.TrustStoreBuilderPlugin'
		}
	}
}

dependencies {
	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.71"

	testImplementation localGroovy()
	testImplementation gradleTestKit()
	testImplementation 'commons-io:commons-io:2.7'
	testImplementation 'com.google.jimfs:jimfs:1.2'      // In-Memory Filesystem
	testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5', {
		exclude group: 'org.codehaus.groovy'
	}
	testImplementation 'org.hamcrest:hamcrest-all:1.3'
}

wrapper {
	gradleVersion = '6.3'
	distributionType = 'all'
}

apply from: 'gradle/versioning.gradle'
apply from: 'gradle/docs.gradle'
apply from: 'gradle/publish.gradle'
apply from: 'gradle/spotless.gradle'
apply from: 'gradle/qa.gradle'
