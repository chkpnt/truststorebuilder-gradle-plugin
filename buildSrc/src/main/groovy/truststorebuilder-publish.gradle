plugins {
    id("maven-publish")
    id("com.gradle.plugin-publish")
}

/*********************************
 * Configure Maven-Artifact
 *********************************/

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier = "sources"
}

publishing {
    publications {
        pluginMaven(MavenPublication) {
            artifact sourcesJar

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    description "${project.description}\r\n\r\nRevision: ${project.revision}\r\nBuild-date: ${project.buildDate}"
                    url 'https://github.com/chkpnt/truststorebuilder-gradle-plugin'
                    scm {
                        url 'https://github.com/chkpnt/truststorebuilder-gradle-plugin'
                        connection 'scm:git:https://github.com/chkpnt/truststorebuilder-gradle-plugin.git'
                        developerConnection 'scm:git:ssh:git@github.com:chkpnt/truststorebuilder-gradle-plugin.git'
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'chkpnt'
                            name 'Gregor Dschung'
                            email 'gregor@chkpnt.de'
                        }
                    }
                }
            }
        }
    }
}

/*********************************
 * Publish to plugins.gradle.org
 *********************************/

pluginBundle {
    website = 'https://github.com/chkpnt/truststorebuilder-gradle-plugin'
    vcsUrl = 'https://github.com/chkpnt/truststorebuilder-gradle-plugin.git'
    description = project.description
    tags = ['truststore', 'certificates', 'ca']

    plugins {
        truststorebuilder {
            displayName = 'Gradle TrustStore-builder plugin'
        }
    }
}
