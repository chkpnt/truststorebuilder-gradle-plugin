import org.gradle.plugins.ide.eclipse.model.*

apply plugin: 'eclipse'

eclipse.classpath.file.whenMerged { Classpath cp ->
	// In order to get the source for the gradleApi()-dependency:
	String gradleHome = gradle.getGradleHomeDir().absolutePath.replace(File.separator, '/')
	String gradleSrc = "${gradleHome}/src"
	cp.entries.each {ClasspathEntry entry ->
		if ((entry in AbstractLibrary) && (entry.library.file.name.startsWith('gradle-'))) {
			def matcher = entry.library.file.name =~ /gradle-(.*?)-(\d.*)/
			String sourcePath= gradleSrc + '/' + matcher[0][1];
			entry.sourcePath = new org.gradle.plugins.ide.eclipse.model.internal.FileReferenceFactory().fromPath(sourcePath)
		}
	}
}

eclipseJdt << {
	// Requirements for the generated file .settings/org.eclipse.jdt.core.prefs:
	// - no comments, because they contain timestamps
	// - sorted entries
	def lines = outputFile.text.readLines().findAll { !it.startsWith('#') }
	outputFile.text = lines.sort().collect{ it + '\n' }.join()
}